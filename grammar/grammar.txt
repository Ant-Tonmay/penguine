program    → "{" function* "}"
function   → "func" IDENTIFIER "(" params? ")" block
params     → param ("," param)*
param      → ("ref" ":")? IDENTIFIER

block          → "{" statement_list "}"
statement_list → statement statement_list | ε

statement →
      print_stmt ";"
    | assignment_stmt ";"
    | for_stmt
    | if_stmt
    | block

print_stmt     → "print" "(" string ")"

if_stmt        → "if" "(" expression ")" block  else_part
else_part      → 
            "else" if_stmt 
            | "else" block
            | ε

assignment_stmt → assignment ("," assignment)*
assignment      → IDENTIFIER "=" expression

for_stmt →
    "for" "(" assignment_stmt ";" expression ";" assignment_stmt ")" block


expression      → logical_or

logical_or      → logical_and ( "||" logical_and )*

logical_and     → bitwise_or  ( "&&" bitwise_or )*

bitwise_or      → bitwise_xor ( "|" bitwise_xor )*

bitwise_xor     → bitwise_and ( "^" bitwise_and )*

bitwise_and     → equality    ( "&" equality )*

equality        → comparison  ( ("==" | "!=") comparison )*

comparison      → shift       ( ("<" | "<=" | ">" | ">=") shift )*

shift           → additive    ( ("<<" | ">>") additive )*

additive        → multiplicative ( ("+" | "-") multiplicative )*

multiplicative  → unary       ( ("*" | "/") unary )*

unary           → ("!" | "-") unary | primary

primary         → NUMBER | STRING | IDENTIFIER | "(" expression ")"